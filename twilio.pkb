create or replace package body twilio is

  /**********************************
    Global variables for Twilio
  ***********************************/  
  g_account_sid        constant varchar2(255) := 'YOUR_ACCOUNT_SID';
  g_auth_str           constant varchar2(255) := 'Basic ' || 'POSTMAN_GENERATED_TOKEN'; -- Not the token from the Twilio console, the one we generated by Postman
  g_twilio_host        constant varchar2(30)  := 'https://api.twilio.com';
  g_twilio_api_version constant varchar2(20)  := '2010-04-01';
  
  /**
   * Send SMS message via Twilio API
   *
   * @example
   * 
   * twilio.send_sms(
   *     p_to_phone_no => '+11231231234'
   *   , p_from_phone_no => '+11231231235'
   *   , p_msg => 'Hello World!'
   * );    
   *
   * @author Cody Reandeau
   * @created 12-Mar-2022
   * @param p_to_phone_no Phone number recieving the message
   * @param p_from_phone_no Phone number sending the message
   * @param p_msg Message to be sent
  */
  procedure send_sms (
      p_to_phone_no   in varchar2
    , p_from_phone_no in varchar2
    , p_msg           in clob  
  )
  is
    l_result          clob;
    l_method_url      varchar2(255) := '/'||g_twilio_api_version||'/Accounts/'||g_account_sid||'/Messages.json';
    l_debug_template  varchar2(4000) := 'twilio.send_sms %0 %1 %2 %3 %4 %5 %6 %7';
  begin
    apex_debug.enable();
    apex_debug.message(l_debug_template, 'START', 'p_to_phone_no', p_to_phone_no, 'p_from_phone_no', p_from_phone_no, 'p_msg', p_msg);
    
    apex_web_service.g_request_headers(1).name  := 'Content-Type';
    apex_web_service.g_request_headers(1).value := 'application/x-www-form-urlencoded';
    apex_web_service.g_request_headers(2).name  := 'Authorization';
    apex_web_service.g_request_headers(2).value := g_auth_str;

    l_result := apex_web_service.make_rest_request (
        p_url         => g_twilio_host||l_method_url
      , p_http_method => 'POST' 
      , p_parm_name   => apex_string.string_to_table('Body:To:From', ':')
      , p_parm_value  => apex_string.string_to_table(p_msg||':'||p_to_phone_no||':'||p_from_phone_no, ':')
    );

    apex_debug.message(l_debug_template, 'l_result', l_result);
    apex_debug.message(l_debug_template, 'END');
    apex_debug.disable();
  exception
    when others then
      apex_debug.error (l_debug_template, 'Unhandled Exception ', sqlerrm);
      raise;
  end send_sms;

end twilio;
/